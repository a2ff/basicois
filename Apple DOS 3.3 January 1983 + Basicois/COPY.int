10 TEXT 
20 REM **************************
30 REM *       DISK COPY        *
40 REM *     COPYRIGHT BY       *
50 REM *  APPLE COMPUTER,INC.   *
60 REM *         1980           *
70 REM **************************
90 PRINT "<CTRL-D>BLOAD COPY.OBJ0":REM A$2C0
100 DIM DK$(20)
110 DIM I$(40):CALL -936:PRINT "     APPLE DISK DUPLICATION PROGRAM":PRINT :PRINT 
120 CALL 704:CS= PEEK (718)/16
130  POKE 715, PEEK (205)+1:REM BUFSTART
140  POKE 716, PEEK (203)-1:REM BUFEND
150 I$=" ORIGINAL":N=CS:A=718:GOSUB 430:MS=N:N= PEEK (720):GOSUB 440:MD=N:A=717
160 I$="DUPLICATE":N=MS:GOSUB 430:SS=N:N=3-MD:GOSUB 440:SD=N
170 FT=0
180 VTAB 5:TAB 24:PRINT "       ":VTAB 8:TAB 24:PRINT "            "
190 CALL 704: POKE 717,SS*16: POKE 718,MS*16: POKE 719,SD: POKE 720,MD
200 VTAB 17: POKE 34,16
210 CALL -936
220 PRINT "-- PRESS 'RETURN' KEY TO BEGIN COPY --";:INPUT I$
225 CALL -936
230 DK$=" ORIGINAL":GOSUB 480:REM GET ORIG DISK
240 VTAB 5:TAB 24: POKE 50,63:PRINT "READING": POKE 50,255
250 CALL 707:REM  READ A HUNK
260 IF  PEEK (713)=2 THEN 390:REM ERROR!
270 VTAB 5:TAB 24:PRINT "       "
280 IF  PEEK (713)=1 THEN 400
290 IF FT THEN 330:REM NOT FIRST RD
300 DK$="DUPLICATE":GOSUB 480:REM GET DUP DISK
310 VTAB 8:TAB 24: POKE 50,63:PRINT "INITIALIZING": POKE 50,255
320 PRINT "<CTRL-D>INIT XXX,S";SS;",D";SD;",V"; PEEK (714):FT=1:GOTO 340
330 DK$="DUPLICATE":GOSUB 480:REM GET DUP DISK
340 VTAB 8:TAB 24: POKE 50,63:PRINT "WRITING";: POKE 50,255:PRINT "     ";
350 CALL 710:REM  WRITE A HUNK
360 VTAB 8:TAB 24:PRINT "       "
370 IF  PEEK (713)#2 THEN 230
380 VTAB 18: POKE 50,63:PRINT "****** UNABLE TO WRITE ******<CTRL-G><CTRL-G>":GOTO 400
390 VTAB 18: POKE 50,63:PRINT "****** UNABLE TO READ ******<CTRL-G><CTRL-G>"
400 VTAB 19: POKE 50,255: POKE -16368,0:PRINT :INPUT "DO YOU WISH TO MAKE ANOTHER COPY?",I$:IF  LEN(I$)=0 THEN 420:IF I$(1,1)="Y" THEN 170
410 IF I$(1,1)#"N" THEN 400
420 TEXT :CALL -936:CALL 672:DEL 90:END 
430 PRINT :TAB 1:PRINT I$;"  SLOT: ";:L=1:H=7:GOSUB 470:PRINT N;: POKE 50,255:GOSUB 450: POKE A,N*16:RETURN 
440 GOSUB 470:PRINT N;: POKE 50,255:TAB 4:PRINT "       DRIVE: ";:L=1:H=2:GOSUB 450:RETURN 
450 K= PEEK (-16384):IF K<128 THEN 450: POKE -16368,0:IF K=141 THEN 460:K=K-176:IF K<L OR K>H THEN 450:N=K
460 TAB 20:PRINT N;"                  ":RETURN 
470  POKE 50,63:TAB 22:PRINT "DEFAULT = ";:RETURN 
480 IF MS#SS THEN RETURN 
490 IF MD#SD THEN RETURN 
500 CALL -936:IF DK$(1,1)="D" THEN PRINT 
510 PRINT "INSERT ";DK$;" DISK AND PRESS RETURN";:INPUT DK$
520 CALL -936
530 RETURN 
